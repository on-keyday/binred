# BNFもどき
# # - コメント
# * - 1回以上の繰り返し
# ? - 存在する場合のみ
# *? or ?* - 0回以上の繰り返し
# [a|b] - aまたはb 
# [a|b]$* - aまたはbの繰り返しで各要素は1回ずつ出現
# ID - 識別子(定義の中に存在しないワード)
# INTEGER - 整数(10進、16進(0x)) 
# NUMBER - 実数(INTEGERを内包)
# STRING - 文字列
# 空白及び行は自動で無視(よって、pythonとかの文法を解析できない)
# (そのうち、LINE及びSPACEをつける予定)
# エスケープシーケンスは無効(これもそのうち(略))
# ROOTから解析が始まる

ROOT:=SEXPR
GLOBALSTMT:=[STRUCT|NAMESPACE]
STRUCT:="struct" ID BASESTRUCT? "{" VARDEF?* "}"
BASESTRUCT:= ":" VARDEF
VARDEF:= ID TYPE
TYPE:=["int"|"bit"|"byte"|"string"| ID]
VARINIT:= ID ["," ID]*? ":=" EXPR 
EXPR:=ASSIGN
ASSIGN:=EQ [["="] ASSIGN]*?
EQ:=ADD [["=="|"!="|">"|"<"|">="|"<="] ADD]*?
ADD:=PRIM [["+"|"-"|"&"|"|"] PRIM]*?
PRIM:=["true"|"false"|"nil"|INTEGER|IDs|"(" EXPR ")"|STRING]
IDs:=ID AFTER?
AFTER:=["." ID|FUNCCALL]*
NAMESPACE:="namespace" ID "{" STMT*? "}"
STMT:=[VARSTMT|IFSTMT|RETURNSTMT|FUNCSTMT|STRUCT|NAMESPACE|IDINIT|EXPR]
FUNCSTMT:="func" ID "(" [VARDEF ["," VARDEF]*? ]? ")" FUNCRETURN? "{" STMT*? "}"
FUNCRETURN:="->" TYPE
IFSTMT:="if" EXPR [";" EXPR]? "{" STMT*? "}"
RETURNSTMT:="return" EXPR
FUNCCALL:="(" [EXPR ["," EXPR]*? ]?")"
VARSTMT:="var" VARDEF


#S式
SEXPR:=[ID|"("SEXPR "." SEXPR ")"|SLIST]
SLIST:="(" SEXPR SEXPR*? ")" 
